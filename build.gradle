plugins {
    id 'org.springframework.boot' version '2.4.4' apply false
    //List of versions for dependencies:
    //https://docs.spring.io/spring-boot/docs/2.4.4/reference/html/appendix-dependency-versions.html
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'java'
    id 'com.github.node-gradle.node' version '2.2.4' apply false
}

ext {
    //variables for versions of some custom dependencies
    springfoxVersion = '3.0.0'
    mapstructVersion = '1.4.2.Final'
    commonsIoVersion = '2.8.0'
    powermockitoVersion = '2.0.9'
    commonsCollectionsVersion = '3.2.1'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    //Changing version in spring.dependency-management
    ext['lombok.version'] = '1.18.20'

    group = 'ru.rgasymov'
    sourceCompatibility = '15'
    version = (findProperty('version') == 'unspecified') ? '0.0.1-SNAPSHOT' : version

    apply plugin: 'java'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
}

jar {
    enabled false
}

build.doLast {
    File explodedDist = mkdir("$buildDir/libs")
    project(':backend-app').tasks.withType(Jar).each { archiveTask ->
        archiveTask.doLast {
            copy {
                from archiveTask.destinationDirectory
                into explodedDist
                rename {
                    "money-manager-${version}.jar"
                }
            }
            println("# copied jars from output of module 'backend' to ./build/libs")
        }
    }
}